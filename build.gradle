/**
 * Phase1 is invoked with:
 *
 *     gradlew -Pbranch=checkout_branch_name -PreleaseVersion=versionToRelease phase1
 *
 * e.g.:
 *
 *     gradlew -Pbranch=GROOVY_2_4_X -PreleaseVersion=2.4.8 phase1
 *
 * Assumes you have the following properties set up, e.g. in ~/.gradle/gradle.properties
 * or using -PpropName=propValue on the command line:
 *
 *     signing.keyId=hexId
 *     signing.password=your_s3cr3t_passphrase
 *     signing.secretKeyRingFile=path_to_secring.gpg
 *     apacheUser=your_username
 *     apachePassword=your_password
 *     bintrayUser=bintray_username
 *     bintrayPassword=bintray_secret_password
 *
 * Phase 2 is invoked with:
 *
 *     gradlew -Pbranch=checkout_branch_name -PreleaseVersion=versionToRelease phase2
 *
 * e.g.:
 *
 *     gradlew -Pbranch=GROOVY_2_4_X -PreleaseVersion=2.4.8 phase2
 *
 * Assumes you have the following properties set up, e.g. in ~/.gradle/gradle.properties
 * or using -PpropName=propValue on the command line:
 *
 *     ciserver.user=ci_login_user
 *     ciserver.password=ci_login_password
 *     gvm.consumerKey=xxxxx
 *     gvm.consumerToken=yyyyy
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
        classpath 'at.bxm.gradleplugins:gradle-svntools-plugin:2.1'
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    }
}

apply from: 'gradle/phase1.gradle'
apply from: 'gradle/phase2.gradle'

ext {
    grgitClass = org.ajoberstar.grgit.Grgit
    repoBase = 'https://git1-us-west.apache.org/repos/asf'
    repoUri = "$repoBase/groovy.git"
    now = new Date().format('yyyy-MM-dd')
}

task phase1 {
    dependsOn 'prepareVoteThread'
    doLast {
        println "Pre-vote phase"
    }
}

task phase2 {
    dependsOn 'announceReleaseOnSDKman'
    doLast {
        println "Post-vote phase"
    }
}
