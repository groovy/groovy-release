import at.bxm.gradleplugins.svntools.tasks.SvnCheckout
import at.bxm.gradleplugins.svntools.tasks.SvnCommit
import at.bxm.gradleplugins.svntools.tasks.SvnAdd

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
        classpath "at.bxm.gradleplugins:gradle-svntools-plugin:2.1"
    }
}

apply plugin: "at.bxm.svntools"

svntools {
    username = svnUsername
    password = svnPassword
}

task init {
    assert project.hasProperty('branch')
    rootProject.ext.branch = project.getProperty('branch')
    rootProject.ext.stagingDir = "$project.buildDir/staging-$branch/"
}

task checkout(dependsOn: init) {
    group = "Pre-vote phase"
    description = "Checks out the Apache Groovy sources (assumes -Pbranch=branch_to_release)"
    // fresh clone may be overkill but take safe option for now unless skipClone specified
    doLast {
        def grgit
        if (rootProject.hasProperty('skipClone')) {
            grgit = grgitClass.open(dir: stagingDir)
        } else {
            println "Cloning $repoUri to $stagingDir. This may take a few minutes ..."
            grgit = grgitClass.clone(dir: stagingDir, uri: repoUri, refToCheckout: branch)
        }
        grgit.checkout(branch: rootProject.getProperty('branch'))
        def id = grgit.head().abbreviatedId
        def message = grgit.head().shortMessage
        def dir = file(stagingDir)
        assert !dir.isFile() && dir.listFiles()
        println "Checked out: $grgit.branch.current.name @ $id ($message)"
    }
}

task confirmVersion {
    group = "Pre-vote phase"
    description = "Confirm version to release (assumes -PreleaseVersion=version_to_release)"
    assert rootProject.hasProperty('releaseVersion')
    rootProject.ext.relVersion = rootProject.getProperty('releaseVersion')
    rootProject.ext.numVersion = relVersion.find(/^[\d\.]+/)
    dependsOn checkout
    doLast {
        def propsFile = file("$stagingDir/gradle.properties")
        def propsText = propsFile.text
        assert propsText.matches(/(?sm).*$numVersion[a-zA-Z\-]*\.SNAPSHOT.*/)
        assert propsText.matches(/(?sm).*$numVersion[a-zA-Z\-]*-SNAPSHOT.*/)
    }
}

task createReleaseBranch {
    group = "Pre-vote phase"
    description = "Creates a release branch"
    dependsOn confirmVersion
    doLast {
        def grgit = grgitClass.open(dir: stagingDir)
        grgit.checkout(branch: "REL-BRANCH-$relVersion", startPoint: branch, createBranch: true)
        println "Checked out: $grgit.branch.current.name"
    }
}

task updateVersionProperties {
    group = "Pre-vote phase"
    description = "Promotes versions to non SNAPSHOT in gradle.properties and commits the result"
    dependsOn createReleaseBranch
    doLast {
        def propsFile = file("$stagingDir/gradle.properties")
        def propsText = propsFile.text
        propsText = propsText.replace(relVersion + '-SNAPSHOT', relVersion)
        propsText = propsText.replace(relVersion + '.SNAPSHOT', relVersion)
        propsFile.text = propsText
        def grgit = grgitClass.open(dir: stagingDir)
        grgit.add(patterns: ['gradle.properties'])
        def commit = grgit.commit(message: "Release $relVersion: update versions")
        println "@ $commit.abbreviatedId ($commit.shortMessage)"
    }
}

task buildAndUpload {
    group = "Pre-vote phase"
    description = "Builds Apache Groovy and publishes the Maven artifacts to the staging repository on Bintray"
    dependsOn updateVersionProperties
    doLast {
        // less declarative than ideal but using 'type: GradleBuild' on the
        // task confuses jarAllWithIndy which does use the declarative approach
        def groovyBuild = project.tasks.create(name: "groovyBuild", type: GradleBuild)
        groovyBuild.dir = file(stagingDir)
        groovyBuild.tasks = ['install', 'dist', 'artifactoryPublish']
        groovyBuild.execute()
    }
}

task cleanSvnWorkspace(type: Delete) {
    dependsOn buildAndUpload
    dependsOn init
    delete "$project.buildDir/svn-workspace-$branch"
}

task prepareSvnWorkspace(type: SvnCheckout, dependsOn: cleanSvnWorkspace) {
    svnUrl = "https://dist.apache.org/repos/dist/dev/groovy"
    workspaceDir = "$project.buildDir/svn-workspace-$branch"
}

task copySvnDistro(type: Copy, dependsOn: prepareSvnWorkspace) {
    description = "Creates the required tree structure for distribution"
        from ("$stagingDir/target/distributions")
        into "$project.buildDir/svn-workspace-$branch/$relVersion/distribution"
        exclude 'apache-groovy-src-*'
    }

task copySvnSources(type: Copy, dependsOn: prepareSvnWorkspace) {
    description = "Creates the required tree structure for sources"
        from ("$stagingDir/target/distributions")
        into "$project.buildDir/svn-workspace-$branch/$relVersion/sources"
        include 'apache-groovy-src-*'
    }

task addSvnFiles(type: SvnAdd, dependsOn: [copySvnDistro, copySvnSources]) {
    description = "Adds the changed files to svn"
    add "$project.buildDir/svn-workspace-$branch/$relVersion/"
    recursive true
}

task commitDevDist(dependsOn: addSvnFiles) {
    group = "Pre-vote phase"
    description = "Commits the changed files to the Apache dist dev server"
    doLast {
        // less declarative than ideal but we want deferred operation since
        // svn plugin assumes list of files to commit is known at config time
        def groovyBuild = project.tasks.create(name: "groovyBuild", type: GradleBuild)
        groovyBuild.startParameter.projectProperties = gradle.startParameter.projectProperties
        groovyBuild.tasks = ['commitDevDistInternal']
        groovyBuild.execute()
    }
}

task commitDevDistInternal(type: SvnCommit) {
    source << "$project.buildDir/svn-workspace-$branch/$relVersion/"
    def tree = fileTree("$project.buildDir/svn-workspace-$branch/$relVersion/").include('**/*')
    tree.visit {
        source << "$project.buildDir/svn-workspace-$branch/$relVersion/$it.path"
    }
    commitMessage = "New version $branch $relVersion added"
}

task signDistribution {
    group = "Pre-vote phase"
    description = "Signs the artifacts"
    dependsOn commitDevDist
}

task createChecksums {
    group = "Pre-vote phase"
    description = "Creates SHA-256 checksums for all artifacts"
    dependsOn signDistribution
}

task uploadToApacheDevServers {
    group = "Pre-vote phase"
    description = "Pushes the artifacts to the Apache DEV subversion repository"
    dependsOn createChecksums
}

task createAndPushTag {
    group = "Pre-vote phase"
    description = "Creates a release tag and pushes it to the Apache Git repository"
    dependsOn uploadToApacheDevServers
}

task releaseOnJira {
    group = "Pre-vote phase"
    description = "Releases the version on JIRA"
    dependsOn createAndPushTag
}

task prepareVoteThread {
    group = "Pre-vote phase"
    description = "Generates a [VOTE] thread to be tweaked and sent to the dev@ mailing list"
    dependsOn releaseOnJira
}


